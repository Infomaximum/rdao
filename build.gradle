plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id ("de.jjohannes.extra-java-module-info") version "0.15"
}
if (project.hasProperty('ossrhUsername')) {
    apply from: 'publish.gradle'
}
apply from: 'https://raw.githubusercontent.com/Infomaximum/platform-java-module-info/main/com.infomaximum.platform-java-module-info.gradle'

group 'com.infomaximum'
version '1.14.1'

ext.moduleName = 'com.infomaximum.rdao'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

compileJava {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath
        ]
        classpath = files()
    }
}

sourceSets {
    main {
        java.srcDir 'src/main/java'
        resources.srcDir 'src/main/resources'
    }
    test {
        java.srcDir 'src/test/java'
        resources.srcDir 'src/test/resources'
    }
    test_performance {
        java.srcDir 'src/test_performance/java'
        resources.srcDir 'src/test_performance/resources'
        compileClasspath += sourceSets.main.output + sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
}

configurations {
    test_performanceImplementation.extendsFrom(testImplementation)
    test_performanceRuntimeOnly.extendsFrom(testRuntimeOnly)
}

repositories {
    mavenCentral()
}

dependencies {

    implementation 'org.rocksdb:rocksdbjni:6.8.1'

    implementation('com.google.guava:guava:31.1-jre') {
        exclude group: 'com.google.guava', module: 'failureaccess'
        exclude group: 'com.google.guava', module: 'listenablefuture'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'com.google.j2objc', module: 'j2objc-annotations'
    }

    implementation 'net.minidev:json-smart:2.4.8'

    implementation 'org.slf4j:slf4j-api:1.7.30'

    testImplementation group: 'junit', name: 'junit', version: '4.13-beta-3'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.5.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.5.2'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.13.2'

    testImplementation 'ch.qos.logback:logback-core:1.2.2'
    testImplementation 'ch.qos.logback:logback-classic:1.2.2'

    testImplementation 'org.apache.commons:commons-io:1.3.2'
    testImplementation 'org.apache.commons:commons-math:2.0'
}

task test_performance(type: Test, description: 'Performance test', group: 'Verification') {}